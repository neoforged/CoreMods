plugins {
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'net.neoforged.gradleutils' version '3.0.0-alpha.10'
    id 'signing'
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'

group 'net.neoforged'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

version = gradleutils.version
gradleutils.version {
    branches.suffixBranch()
}

ext.ASM_VERSION = 9.3

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

test {
    exclude '**/*'
    useJUnitPlatform()
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileTestJava {
    exclude '**/*'
}

repositories {
    maven {
        name = 'neoforged'
        url = 'https://maven.neoforged.net/releases'
    }
}

sourceSets {
    testJars
}

changelog {
    from "6.0"
}
dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.+')
    testImplementation('org.powermock:powermock-core:2.0.+')
    testImplementation('org.hamcrest:hamcrest-core:2.2')
    testImplementation('org.apache.logging.log4j:log4j-core:2.17.1')
    testCompileOnly('org.jetbrains:annotations:21.0.1')
    compileOnly('org.jetbrains:annotations:21.0.1')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.7.+')
    testRuntimeOnly(sourceSets.testJars.output)
    implementation('cpw.mods:modlauncher:10.1.10')
    implementation('org.apache.logging.log4j:log4j-api:2.14.1')
    implementation("org.ow2.asm:asm:${ASM_VERSION}")
    implementation("org.ow2.asm:asm-commons:${ASM_VERSION}")
    implementation("org.ow2.asm:asm-tree:${ASM_VERSION}")
    implementation("org.ow2.asm:asm-util:${ASM_VERSION}")
    implementation('org.openjdk.nashorn:nashorn-core:15.4')
}

ext.sharedManifest = manifest {
    attributes(['Specification-Title': 'coremods',
                'Specification-Vendor': 'neoforged',
                'Specification-Version': '1', // Currently version 1 of the coremod specification
                'Implementation-Title': project.name,
                'Implementation-Version': "${project.version}+${gradleutils.gitInfo.branch}.${gradleutils.gitInfo.abbreviatedId}",
                'Implementation-Vendor' :'neoforged',
                'Git-Commit': gradleutils.gitInfo.abbreviatedId],
            'net/neoforged/coremod/')
}

jar {
    manifest = project.manifest {
        from sharedManifest
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                from components.java
                artifact sourcesJar
                artifactId = "coremods"
                name = 'Core Mods'
                description = 'Core modding framework for use with forge'
                url = 'https://github.com/neoforged/coremods'
                scm {
                    url = 'https://github.com/neoforged/coremods'
                    connection = 'scm:git:git://github.com/neoforged/coremods.git'
                    developerConnection = 'scm:git:git@github.com:neoforged/coremods.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/neoforged/coremods/issues'
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                }
            }
        }
    }
    repositories {
        maven gradleutils.publishingMaven
    }
}

if (System.getenv('GPG_PRIVATE_KEY')) {
    signing {
        final signingKey = System.getenv('GPG_PRIVATE_KEY') ?: ''
        final signingPassword = System.getenv('GPG_KEY_PASSWORD') ?: ''
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }
}
